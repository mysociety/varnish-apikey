varnishtest "Test throttled client IP with default max"

server s1 {
  rxreq
  txresp -body "hello world"
} -start

varnish v1 -vcl {
  import std;
  include "${pwd}/vcl/varnish-apikey.vcl";

  backend default {
    .host = "${s1_addr}";
    .port = "${s1_port}";
  }

  sub recognize_apiname_apikey_token {
    # Identify api
    set req.http.apiname = "testthrottledbyipapidefault";
    # Save apikey
    set req.http.apikey = regsub(req.url, ".*[?;]apikey=([^;]*).*", "\1");
  }

  sub set_up {
    redis.command("FLUSHDB");
    redis.execute();

    redis.command("SET");
    redis.push("api:testthrottledbyipapidefault:throttled");
    redis.push("1");
    redis.execute();

    # Set up all the default throttling settings for the api
    redis.command("SET");
    redis.push("api:testthrottledbyipapidefault:counter:time");
    redis.push("60");
    redis.execute();

    redis.command("SET");
    redis.push("api:testthrottledbyipapidefault:blocked:time");
    redis.push("60");
    redis.execute();

    # Set the default_max for the api to 1, but don't set a specific max for
    # the user
    redis.command("SET");
    redis.push("api:testthrottledbyipapidefault:default_max");
    redis.push("1");
    redis.execute();
  }

  sub vcl_init {
    redis.init("main", "${redis_master_address}", 500, 0, 0, 0, 0, false, 1);
  }

  sub vcl_recv {
    # Hacky way to run the setup only on the first request because we can't
    # call it from vcl_init
    if (req.http.X-Test-Setup == "true") {
      call set_up;
    }
    # Validate apikey using apikey library.
    call validate_api;
    set req.backend = default;
  }
} -start

client c1 {
  # The first request should be accepted, and will set the counter for this
  # IP to 0, as well as mark the counter as needing to be reset in 60
  # seconds
  txreq -hdr "X-Test-Setup: true"
  rxresp

  expect resp.status == 200

  # The second request should also be accepted, as it will set the counter for
  # this IP to 1
  txreq
  rxresp

  expect resp.status == 200

  # The third request should fail, as it will set the counter for
  # this IP to 2, which is greater than the max we've set
  txreq
  rxresp

  expect resp.status == 401

} -run

varnish v1 -expect client_req == 3

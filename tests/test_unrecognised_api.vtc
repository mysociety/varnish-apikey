varnishtest "Test unrecognised APIs"

server s1 {
  rxreq
  txresp -body "hello world"
} -start

varnish v1 -vcl {
  import std;
  import redis;

  sub vcl_init {
    new db = redis.db(
        location="${redis_master_address}",
        type=master,
        connection_timeout=500,
        shared_connections=false,
        max_connections=1);
  }

  include "${pwd}/vcl/varnish-apikey.vcl";

  backend default {
    .host = "${s1_addr}";
    .port = "${s1_port}";
  }

  sub recognize_apiname_apikey_token {
    # Purposely doing nothing
  }

  sub vcl_recv {
    # Validate apikey using apikey library.
    call validate_api;
    set req.backend_hint = default;
  }
} -start

client c1 {
  txreq
  rxresp

  expect resp.status == 200
  expect resp.bodylen == 11
} -run

varnish v1 -expect client_req == 1

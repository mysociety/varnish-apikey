varnishtest "Test no api key"

server s1 {
  rxreq
  txresp -body "hello world"
} -start

varnish v1 -vcl {
  import std;
  import redis;

  sub vcl_init {
    new db = redis.db(
        location="${redis_master_address}",
        type=master,
        connection_timeout=500,
        shared_connections=false,
        max_connections=1);
  }

  include "${pwd}/vcl/varnish-apikey.vcl";

  backend default {
    .host = "${s1_addr}";
    .port = "${s1_port}";
  }

  sub recognize_apiname_apikey_token {
    # Identify api
    set req.http.apiname = "testapi";
    # Save apikey
    set req.http.apikey = regsub(req.url, ".*[?;]apikey=([^;]*).*", "\1");
  }

  sub set_up {
    db.command("FLUSHDB");
    db.execute();

    db.command("SET");
    db.push("api:testapi:restricted");
    db.push("1");
    db.execute();
  }

  sub vcl_recv {
    call set_up;
    # Validate apikey using apikey library.
    call validate_api;
    set req.backend_hint = default;
  }
} -start

client c1 {
  txreq
  rxresp

  expect resp.status == 401
} -run

varnish v1 -expect client_req == 1

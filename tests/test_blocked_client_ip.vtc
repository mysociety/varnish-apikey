varnishtest "Test blocked IP address"

server s1 {
  rxreq
  txresp -body "hello world"
} -start

varnish v1 -vcl {
  import std;
  include "${pwd}/vcl/varnish-apikey.vcl";

  backend default {
    .host = "${s1_addr}";
    .port = "${s1_port}";
  }

  sub recognize_apiname_apikey_token {
    # Identify api
    set req.http.apiname = "testapiipblocked";
    # Save apikey
    set req.http.apikey = regsub(req.url, ".*[?;]apikey=([^;]*).*", "\1");
  }

  sub set_up {
    redis.send("FLUSHDB");
    redis.send("SET api:testapiipblocked:throttled 1");
    redis.send("SET api:testapiipblocked:counter:time 60");
    redis.send("SET api:testapiipblocked:blocked:time 60");
    redis.send("SET api:testapiipblocked:default_max 60");
    redis.send("SETEX key:127.0.0.1:api:testapiipblocked:blocked 60 1");
  }

  sub vcl_init {
    call set_up;
  }

  sub vcl_recv {
    # Validate apikey using apikey library.
    call validate_api;
    set req.backend_hint = default;
  }
} -start

client c1 {
  txreq
  rxresp

  expect resp.status == 401
} -run

varnish v1 -expect client_req == 1
